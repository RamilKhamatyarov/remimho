plugins {
    id("application")
    id("io.quarkus") version "3.29.0"
    id("org.jetbrains.kotlin.jvm") version "2.2.21"
    id("com.diffplug.spotless") version "8.0.0"
    id("com.gluonhq.gluonfx-gradle-plugin") version "1.0.28"
}

group = "ru.rkhamatyarov"
version = "1.0.0"

repositories {
    mavenCentral()
}

def javafxVersion = '17.0.8'

def osName = System.getProperty('os.name').toLowerCase()
def platform = osName.contains('win') ? 'win' : osName.contains('mac') ? 'mac' : 'linux'

dependencies {
    implementation(enforcedPlatform("io.quarkus.platform:quarkus-bom:3.28.5"))
    implementation("io.quarkus:quarkus-arc")
    implementation("io.quarkus:quarkus-config-yaml")
    implementation("io.quarkus:quarkus-core")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.10.2")

    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:${platform}"

    testImplementation("org.junit.jupiter:junit-jupiter-api:6.0.0")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:6.0.0")
    testImplementation("org.junit.jupiter:junit-jupiter-params:6.0.0")
    testImplementation("org.testfx:testfx-junit5:4.0.18")
    testImplementation("org.jetbrains.kotlin:kotlin-reflect:2.2.21")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5:2.2.21")
    testImplementation("io.quarkus:quarkus-junit5")
    testImplementation("io.mockk:mockk:1.14.6")
}

application {
    mainClass = 'ru.rkhamatyarov.Main'
}

tasks.register('runJavaFx', JavaExec) {
    group = 'application'
    mainClass = 'ru.rkhamatyarov.Main'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            '--module-path', configurations.runtimeClasspath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.graphics,javafx.base'
    ]
}

tasks.test {
    useJUnitPlatform()
}

gluonfx {
    target = 'host'

    compilerArgs = [
            '--initialize-at-build-time=org.jboss.logmanager',
            '--initialize-at-build-time=org.jboss.logging',
            '--initialize-at-build-time=io.quarkus.bootstrap',
            '--initialize-at-build-time=io.quarkus.bootstrap.logging.',
            '--initialize-at-build-time=io.quarkus.runtime.logging',
            '--initialize-at-build-time=io.smallrye.common',
            '-H:+UnlockExperimentalVMOptions'
    ]

    reflectionList = [
            'ru.rkhamatyarov.Main',
            'ru.rkhamatyarov.engine.GameLoop',
            'ru.rkhamatyarov.model.GameState',
            'ru.rkhamatyarov.model.GameOfLifeGrid',
            'ru.rkhamatyarov.engine.Whiteboard'
    ]

    resourcesList = [
            'META-INF/.*',
            'application.properties',
            '.*\\\\.properties'
    ]

    bundlesList = [
            'com.sun.javafx.scene.control.skin.resources.controls'
    ]
}

kotlin {
    compilerOptions {
        freeCompilerArgs.add("-Xjvm-default=all")
    }
}

spotless {
    kotlin {
        ktlint()
        target("**/*.kt")
        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }
    kotlinGradle {
        target("*.gradle.kts")
        ktlint()
    }
}
