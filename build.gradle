plugins {
    id("application")
    id("io.quarkus") version "3.28.0"
    id("org.jetbrains.kotlin.jvm") version "2.2.20"
    id("com.diffplug.spotless") version "7.2.1"
}

group = "ru.rkhamatyarov"
version = "1.0.0"

repositories {
    mavenCentral()
}

def javafxVersion = '17.0.8'

def osName = System.getProperty('os.name').toLowerCase()
def platform = osName.contains('win') ? 'win' : osName.contains('mac') ? 'mac' : 'linux'

dependencies {
    implementation(enforcedPlatform("io.quarkus.platform:quarkus-bom:3.26.3"))
    implementation("io.quarkus:quarkus-arc")
    implementation("io.quarkus:quarkus-config-yaml")
    implementation("io.quarkus:quarkus-core")
    implementation("io.quarkus:quarkus-rest")
    implementation("org.jetbrains.kotlin:kotlin-stdlib")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.10.2")
    implementation ("com.pinterest:ktlint:0.51.0-FINAL") {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'
    }

    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-fxml:${javafxVersion}:${platform}"


    testImplementation("org.junit.jupiter:junit-jupiter-api:5.13.4")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.13.4")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.13.4")
    testImplementation("org.testfx:testfx-junit5:4.0.18")
    testImplementation("org.jetbrains.kotlin:kotlin-reflect:2.2.20")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5:2.2.20")
    testImplementation("io.quarkus:quarkus-junit5")
    testImplementation("io.mockk:mockk:1.14.5")
}

application {
    mainClass = 'ru.rkhamatyarov.Main'
}

tasks.register('runJavaFx', JavaExec) {
    group = 'application'
    mainClass = 'ru.rkhamatyarov.Main'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = [
            '--module-path', configurations.runtimeClasspath.asPath,
            '--add-modules', 'javafx.controls,javafx.fxml,javafx.graphics,javafx.base'
    ]
}

tasks.test {
    useJUnitPlatform()
}

kotlin {
    compilerOptions {
        freeCompilerArgs.add("-Xjvm-default=all")
    }
}

spotless {
    kotlin {
        ktlint()
        target("**/*.kt")
        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }
    kotlinGradle {
        target("*.gradle.kts")
        ktlint()
    }
}
